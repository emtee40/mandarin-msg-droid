apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            if (project.hasProperty("storeFile")) storeFile file("$rootDir/" + project.storeFile)
            if (project.hasProperty("storePassword")) storePassword project.storePassword
            if (project.hasProperty("keyAlias")) keyAlias project.keyAlias
            if (project.hasProperty("keyPassword")) keyPassword project.keyPassword
        }
    }
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.tomclaw.mandarin"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode project.hasProperty("versionCode") ? Integer.parseInt(project.versionCode) : 280
        versionName "1.16"
        resConfigs "ru", "en", "be", "uk", "kk"
    }
    splits {
        density {
            enable false
            reset()
            include "nodpi", "hdpi", "xhdpi", "xxxhdpi"
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFile 'proguard-rules.pro'
        }
    }
    lintOptions {
        warningsAsErrors false
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation project(':smileys')
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.akexorcist:RoundCornerProgressBar:2.0.3'
    implementation 'com.github.mtotschnig:StickyListHeaders:2.7.1'
    implementation 'org.zakariya.stickyheaders:stickyheaders:0.7.6'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.okhttp3:okhttp:3.0.1'
}

/// OPTIMIZATION ///

android {
    packagingOptions {
        exclude "META-INF/androidx.activity_activity.version"
        exclude "META-INF/androidx.appcompat_appcompat-resources.version"
        exclude "META-INF/androidx.appcompat_appcompat.version"
        exclude "META-INF/androidx.cardview_cardview.version"
        exclude "META-INF/androidx.coordinatorlayout_coordinatorlayout.version"
        exclude "META-INF/androidx.core_core.version"
        exclude "META-INF/androidx.customview_customview.version"
        exclude "META-INF/androidx.drawerlayout_drawerlayout.version"
        exclude "META-INF/androidx.fragment_fragment.version"
        exclude "META-INF/androidx.interpolator_interpolator.version"
        exclude "META-INF/androidx.lifecycle_lifecycle-runtime.version"
        exclude "META-INF/androidx.lifecycle_lifecycle-viewmodel.version"
        exclude "META-INF/androidx.recyclerview_recyclerview.version"
        exclude "META-INF/androidx.savedstate_savedstate.version"
        exclude "META-INF/androidx.vectordrawable_vectordrawable.version"
        exclude "META-INF/androidx.viewpager_viewpager.version"
        exclude "META-INF/com.google.android.material_material.version"
    }
}
configurations {
    all {
        exclude group: "androidx.arch.core", module: "core-runtime"
        exclude group: "androidx.cursoradapter", module: "cursoradapter"
        exclude group: "androidx.exifinterface", module: "exifinterface"
        exclude group: "androidx.lifecycle", module: "lifecycle-livedata"
        exclude group: "androidx.lifecycle", module: "lifecycle-livedata-core"
        exclude group: "androidx.loader", module: "loader"
        exclude group: "androidx.transition", module: "transition"
        exclude group: "androidx.vectordrawable", module: "vectordrawable-animated"
        exclude group: "androidx.versionedparcelable", module: "versionedparcelable"
        exclude group: "androidx.viewpager2", module: "viewpager2"
    }
}
dependencies {
    implementation 'androidx.interpolator:interpolator:1.0.0'
}